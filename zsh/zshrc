#====================
# Prompt
#====================

autoload colors; colors;
setopt prompt_subst
autoload -Uz vcs_info

get_git_status() {
  changes="$(git status -s)"

  untracked="$(echo "$changes" | grep '^??' | wc -l)"
  modifed="$(echo "$changes" | grep -E '^ (D|M)' | wc -l)"
  new="$(echo "$changes" | grep -E '^(A|M)' | wc -l)"

  if [[ -z "$changes" ]]; then
    echo -n " %{$fg_bold[green]%}✓"
  else
    echo -n ' '
    ((untracked > 0 )) && echo -n "%{$fg_bold[red]%}●"
    ((modifed > 0 ))   && echo -n "%{$fg_bold[yellow]%}●"
    ((new > 0 ))       && echo -n "%{$fg_bold[green]%}●"
  fi
}

# or use pre_cmd, see man zshcontrib
vcs_info_wrapper() {
  vcs_info
  if [[ -n "$vcs_info_msg_0_" ]]; then
    echo -n "${vcs_info_msg_0_}" # (branch
    get_git_status               # ✓|●
    echo "%{$fg_bold[grey]%})"   # )
  fi
}

# print username@ when connected over SSH
username() {
  [[ ! -z "$SSH_CONNECTION" ]] && echo "%n%B%F{004}@%f%b"
}

zstyle ':vcs_info:*' enable git
zstyle ':vcs_info:git*' formats " %{$fg_bold[grey]%}(%{$reset_color%}%b"

PROMPT='$(username)%m %{${fg_bold[green]}%}%5~$(vcs_info_wrapper)%{${fg_bold[blue]}%} »%{$reset_color%} '

#====================
# Completion
#====================

# Modern completion system
autoload -Uz compinit
compinit

zstyle ':completion:*' auto-description 'specify: %d'
zstyle ':completion:*' completer _expand _complete _correct _approximate
zstyle ':completion:*' format 'Completing %d'
zstyle ':completion:*' group-name ''
zstyle ':completion:*' menu select=2
eval "$(dircolors -b)"
zstyle ':completion:*:default' list-colors ${(s.:.)LS_COLORS}
zstyle ':completion:*' list-colors ''
zstyle ':completion:*' list-prompt %SAt %p: Hit TAB for more, or the character to insert%s
zstyle ':completion:*' matcher-list '' 'm:{a-z}={A-Z}' 'm:{a-zA-Z}={A-Za-z}' 'r:|[._-]=* r:|=* l:|=*'
zstyle ':completion:*' select-prompt %SScrolling active: current selection at %p%s
zstyle ':completion:*' use-compctl false
zstyle ':completion:*' verbose true
zstyle ':completion:*:*:kill:*:processes' list-colors '=(#b) #([0-9]#)*=0=01;31'
zstyle ':completion:*:kill:*' command 'ps -u $USER -o pid,%cpu,tty,cputime,cmd'


#====================
# General
#====================

# Keeps 1 000 lines of history, saved in ~/.zsh_history
HISTSIZE=1000
SAVEHIST=1000
HISTFILE=~/.zsh_history

# ctrl + r : search in history
bindkey '^R' history-incremental-search-backward

# Move with ctrl + arrow
bindkey '^[[1;5C' emacs-forward-word
bindkey '^[[1;5D' emacs-backward-word

# Move with beginning-of-line and end-of-line
bindkey "\e[H" beginning-of-line
bindkey "\e[F" end-of-line

# "cd" optional
#setopt autocd

# Corrects bad commands
#setopt correctall

# No duplication in history
setopt hist_ignore_all_dups

# Shared history between sessions, ignoring duplicates
setopt histignorealldups sharehistory

# Use Ctrl-x-e to edit the current command line
autoload -U edit-command-line
zle -N edit-command-line
bindkey '^xe' edit-command-line
bindkey '^x^e' edit-command-line

# Syntax highlighting
source ~/.zsh/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh


#====================
# Exports
#====================

# coloring man pages (with less)
export LESS_TERMCAP_mb=$'\E[01;31m'
export LESS_TERMCAP_md=$'\E[01;31m'
export LESS_TERMCAP_me=$'\E[0m'
export LESS_TERMCAP_se=$'\E[0m'
export LESS_TERMCAP_so=$'\E[01;44;33m'
export LESS_TERMCAP_ue=$'\E[0m'
export LESS_TERMCAP_us=$'\E[01;32m'

export VISUAL='nvim -R'
export EDITOR=nvim
export PAGER='less -iR'

# 256 colors
export TERM='xterm-256color'


#====================
# Aliases
#====================

alias cp='cp -i'
alias mv='mv -i'
alias ls='ls --color --group-directories-first -Fh'
alias ll='ls -l'
alias la='ls -A'
alias lla='ll -A'
alias grep='grep --color -I --exclude-dir=.git'
alias tree='tree -C --dirsfirst'
alias df='df -hT'
alias du='du -sh'
alias ip='ip -c'
alias ipaddr='ip -c -4 -br addr'
alias find='noglob find'
alias pingg="ping www.google.com"
alias o='xdg-open'
# Display the 20 biggest files
alias dub='du -sh * | sort -rh | head -n 20'

alias -s pdf='xdg-open'
alias -s {odt,doc,docx,ods,xls,xlsx,odp,ppt,pptx}='xdg-open'
alias -s {jpg,jpeg,png,gif}='xdg-open'

# Debian
if [ -f /etc/debian_version ]; then
  alias apt='noglob apt'
  alias upgrade="sudo apt upgrade"
  alias find-local-packages='apt list -i 2> /dev/null  | grep "local]$" | cut -d '/' -f 1'

  # https://bugs.debian.org/cgi-bin/bugreport.cgi?bug=934587#25
  export PATH="$PATH:/usr/lib/cargo/bin/"
  alias fd='noglob fd'

  if [[ -x "$(whence -p aptlu)" ]] ; then
    alias update="sudo apt update && aptlu"
  else
    alias update="sudo apt update && apt list --upgradable"
  fi
fi

# Arch Linux
if [ -f /etc/arch-release ]; then
  alias find-pacnew='find / -regextype posix-extended -regex ".+\.pac(new|save)" -print -o -type d -name "home" -prune 2> /dev/null'
  alias fd='noglob fd'
fi

if [ -f ~/.zsh_aliases ]; then
  source ~/.zsh_aliases
fi

if [ -f ~/.zsh_functions ]; then
  source ~/.zsh_functions
fi
